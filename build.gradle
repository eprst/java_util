plugins {
  id 'com.github.ben-manes.versions' version '0.27.0' // adds 'dependencyUpdates'
  id "me.champeau.gradle.jmh" version "0.5.0"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
  mavenCentral()
}

dependencies {
  jmh 'com.google.guava:guava:28.2-jre'
  jmh 'org.apache.commons:commons-text:1.8'
  jmh sourceSets.test.output

  testImplementation 'com.google.guava:guava:28.2-jre'
  testImplementation 'org.apache.commons:commons-text:1.8'
  testImplementation 'junit:junit:4.13'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
ext.canSign = project.hasProperty('signing.password')

compileJava {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

javadoc {
  options.addStringOption('Xdoclint:none', '-quiet')
}

test {
  afterTest { desc, result ->
    logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
  }
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

tasks.withType(Sign) {
  onlyIf { canSign }
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Murmur3'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Fast Murmur3 hash implementation for Java'
        url 'https://github.com/eprst/murmur3'

        scm {
          connection 'scm:git:git://github.com/eprst/murmur.git'
          developerConnection 'scm:git:git@github.com/eprst/murmur.git'
          url 'https://github.com/eprst/murmur3'
        }

        licenses {
          license {
            name 'Unlicense'
            url 'https://unlicense.org'
          }
        }

        developers {
          developer {
            id 'eprst'
            name 'Konstantin Sobolev'
            email 'konstantin.sobolev@gmail.com'
          }
        }
      }
    }
  }
}
